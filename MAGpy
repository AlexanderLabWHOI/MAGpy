shell.executable("/bin/bash")
shell.prefix("source $HOME/.bashrc; ")

IDS, = glob_wildcards("mags/{id}.fa")

rule all:
	input: 'output.txt'

rule checkm:
	input: "mags"
	output: "checkm.txt"
	threads: 16
	shell: "checkm.sh lineage_wf -f checkm.txt -t {threads} -x fa {input} ./checkm"

rule prodigal:
        input: 'mags/{id}.fa'
        output: 'proteins/{id}.faa'
        shell: 'touch {output} && prodigal -a {output} -q -i {input}'

rule diamond:
        input: 'proteins/{id}.faa'
        output: 'diamond/{id}.diamond.tsv'
        threads: 16
        params:
                db="/full/path/to/uniprot_trembl.diamond.dmnd",
                of="6 qseqid sseqid stitle pident qlen slen length mismatch gapopen qstart qend sstart send evalue bitscore"
        shell: "diamond.sh blastp --threads {threads} --max-target-seqs 10 --db {params.db} --query {input} --outfmt {params.of} --out {output}"

rule diamond_report:
	input: 'diamond/{id}.diamond.tsv'
	output: 'diamond_report/bin.{id}.tsv', 'diamond_report/con.{id}.tsv'
	params:
		outdir="diamond_report"
	shell: "diamond_report.pl {input} {params.outdir}"

rule sourmash_sig:
        input: 'mags/{id}.fa'
        output: 'sourmash/{id}.sig'
        shell: "sourmash.sh compute --scaled 1000 -k 31 -o {output} {input}"

rule sourmash_gather:
        input: 'sourmash/{id}.sig'
        output:
                csv='sourmash/{id}.csv',
                out='sourmash/{id}.sm'
	params:
		gb="/full/path/to/genbank-k31.sbt.json",
	shell: "sourmash.sh gather -k 31 {input} {params.gb} -o {output.out} > {output.csv}"

rule pfam_scan:
	input: 'proteins/{id}.faa'
	output: 'pfam/{id}.pfam'
	threads: 8
	params:
		pfam="/full/path/to/pfam_dir/"
	shell: "pfam_scan.sh -outfile {output} -as -cpu {threads} -fasta {input} -dir {params.pfam}"

rule phylophlan_link:
	input: expand("proteins/{sample}.faa", sample=IDS)
	output: "tree"
	threads: 16
	params:
		pdir="/full/path/to/phylophlan_dir/"
	shell: "CWD=`pwd`; ln -s $CWD/proteins {params.pdir}/input/MAGpy && cd {params.pdir} && phylophlan.sh -u MAGpy --nproc {threads} && mv {params.pdir}/output/MAGpy $CWD/tree && cd $CWD && rm {params.pdir}/input/MAGpy"

rule merge:
        input: expand("pfam/{sample}.pfam", sample=IDS), expand("sourmash/{sample}.sm", sample=IDS), expand("diamond_report/bin.{sample}.tsv", sample=IDS), "checkm.txt", "tree"
        output: 'output.txt'
        shell: 'ls {input} >> {output}'




